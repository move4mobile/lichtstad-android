apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.github.triplet.play'
apply plugin: 'io.fabric'

ext.firebaseUIVersion = "4.0.1"
ext.archVersion = "1.1.1"

def buildNumber = (System.getenv("TRAVIS_BUILD_NUMBER") ?: "1") as Integer
def playTrack = System.getenv('PLAY_TRACK') ?: 'alpha'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "nl.gramsbergen.oranjevereniging.lichtstad"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode buildNumber
        versionName "2018.2"

        resConfigs "nl"

        buildConfigField 'java.util.TimeZone', 'EVENT_TIMEZONE', 'java.util.TimeZone.getTimeZone("Europe/Amsterdam")'
        buildConfigField 'String', 'EVENT_YEAR', '"2018"'
    }

    signingConfigs {
        release {
            storeFile file("keystore.jks")
            storePassword System.getenv('KEYSTORE_PASSWORD')
            keyAlias "lichtstad"
            keyPassword System.getenv('KEYSTORE_PASSWORD')
        }
    }

    buildTypes {
        debug {
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable false
        }
    }

    flavorDimensions "app"
    productFlavors {
        lichtstad {
            applicationId "nl.gramsbergen.oranjevereniging.lichtstad"
            versionCode buildNumber
            versionName "2018.2"
        }

        dmr {
            applicationId "nl.combibaangramsbergen.dmr"
            versionCode buildNumber
            versionName "2018.3"
        }
    }

    dataBinding {
        enabled true
    }

    lintOptions {
        lintConfig file("lint.xml")
        warningsAsErrors true
        textReport true
        textOutput "stdout"
    }

    play {
        jsonFile file('play-keys.json')
        track playTrack
        untrackOld playTrack == 'beta'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.0-rc01'
    implementation 'androidx.cardview:cardview:1.0.0-rc01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha1'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.0.0-rc01'
    implementation 'androidx.arch.core:core-runtime:2.0.0-rc01'
    implementation 'androidx.arch.core:core-common:2.0.0-rc01'
    implementation 'com.google.android.material:material:1.0.0-rc01'
    implementation "com.google.android.gms:play-services-maps:15.0.1"
    implementation 'com.google.maps.android:android-maps-utils:0.5'
    implementation "com.google.firebase:firebase-core:16.0.1"
    implementation "com.google.firebase:firebase-database:16.0.1"
    implementation "com.google.firebase:firebase-perf:16.0.0"
    implementation "com.firebaseui:firebase-ui-database:$firebaseUIVersion"
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.github.chrisbanes:PhotoView:2.0.0'
    implementation 'com.squareup.moshi:moshi:1.6.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true
    }
}

apply plugin: 'com.google.gms.google-services'
