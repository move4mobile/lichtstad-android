apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

ext.supportVersion = "25.3.0"
ext.firebaseVersion = "10.2.0"
ext.firebaseUIVersion = "1.2.0"

def buildNumber = (System.getenv("TRAVIS_BUILD_NUMBER") ?: "1") as Integer
def playTrack = System.getenv('PLAY_TRACK') ?: 'alpha'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "nl.gramsbergen.oranjevereniging.lichtstad"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode buildNumber
        versionName "1.0"

        resConfigs "nl"

        buildConfigField 'java.util.TimeZone', 'EVENT_TIMEZONE', 'java.util.TimeZone.getTimeZone("CEST")'
    }

    signingConfigs {
        release {
            storeFile file("keystore.jks")
            storePassword System.getenv('KEYSTORE_PASSWORD')
            keyAlias "lichtstad"
            keyPassword System.getenv('KEYSTORE_PASSWORD')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            debuggable false
        }
    }

    dataBinding {
        enabled true
    }

    lintOptions {
        lintConfig file("lint.xml")
        warningsAsErrors true
    }

    play {
        jsonFile file('play-keys.json')
        track playTrack
        untrackOld playTrack == 'beta'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:support-v13:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:percent:$supportVersion"
    compile "com.google.firebase:firebase-core:$firebaseVersion"
    compile "com.google.firebase:firebase-database:$firebaseVersion"
    compile "com.google.firebase:firebase-crash:$firebaseVersion"
    compile "com.firebaseui:firebase-ui-database:$firebaseUIVersion"

    compile "com.squareup.picasso:picasso:2.5.2"
    compile 'com.android.support.constraint:constraint-layout:1.0.1'
}

apply plugin: 'com.google.gms.google-services'