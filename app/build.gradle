plugins {
    id('com.android.application')
    id('com.github.triplet.play') version '2.2.0'
    id('com.google.firebase.crashlytics')
}

ext.firebaseUIVersion = "6.3.0"
ext.lifecycleVersion = "2.4.1"

def buildNumber = (System.getenv("TRAVIS_BUILD_NUMBER") ?: "1") as Integer
def playTrack = System.getenv('PLAY_TRACK') ?: 'alpha'

android {
    compileSdkVersion 32
    defaultConfig {
        applicationId "nl.gramsbergen.oranjevereniging.lichtstad"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode buildNumber
        versionName "2022.1"
        resConfigs 'nl'
    }

    signingConfigs {
        release {
            storeFile file("keystore.jks")
            storePassword System.getenv('KEYSTORE_PASSWORD')
            keyAlias "lichtstad"
            keyPassword System.getenv('KEYSTORE_PASSWORD')
        }
    }

    buildTypes {
        debug {
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable false
        }
    }

    buildFeatures {
        dataBinding true
    }

    flavorDimensions "app"
    productFlavors {
        lichtstad {
            dimension "app"
            applicationId "nl.gramsbergen.oranjevereniging.lichtstad"
        }
        dmr {
            dimension "app"
            applicationId "nl.combibaangramsbergen.dmr"
        }
        triathlon {
            dimension "app"
            applicationId "nl.triathlonhardenberg.app"
            buildConfigField 'String', 'EVENT_YEAR', '"2022"'
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lint {
        lintConfig file('lint.xml')
        textOutput file('stdout')
        textReport true
        warningsAsErrors true
    }
}

play {
    serviceAccountCredentials file('play-keys.json')
    track playTrack
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    implementation 'androidx.arch.core:core-runtime:2.1.0'
    implementation 'androidx.arch.core:core-common:2.1.0'
    implementation 'com.google.android.material:material:1.6.1'
    implementation "com.google.android.gms:play-services-maps:18.1.0"
    implementation 'com.google.maps.android:android-maps-utils:0.5'
    implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-database'
    implementation 'com.google.firebase:firebase-messaging'
    implementation "com.firebaseui:firebase-ui-database:8.0.1"
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.squareup.moshi:moshi:1.6.0'
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.5.0"

    implementation platform('com.google.firebase:firebase-bom:30.3.0')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
}

apply plugin: 'com.google.gms.google-services'
