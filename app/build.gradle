apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.github.triplet.play'
apply plugin: 'io.fabric'

ext.supportVersion = "27.1.1"
ext.firebaseUIVersion = "4.0.1"
ext.archVersion = "1.1.1"

def buildNumber = (System.getenv("TRAVIS_BUILD_NUMBER") ?: "1") as Integer
def playTrack = System.getenv('PLAY_TRACK') ?: 'alpha'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "nl.gramsbergen.oranjevereniging.lichtstad"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode buildNumber
        versionName "1.0"

        resConfigs "nl"

        buildConfigField 'java.util.TimeZone', 'EVENT_TIMEZONE', 'java.util.TimeZone.getTimeZone("Europe/Amsterdam")'
    }

    signingConfigs {
        release {
            storeFile file("keystore.jks")
            storePassword System.getenv('KEYSTORE_PASSWORD')
            keyAlias "lichtstad"
            keyPassword System.getenv('KEYSTORE_PASSWORD')
        }
    }

    buildTypes {
        debug {
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable false
        }
    }

    dataBinding {
        enabled true
    }

    lintOptions {
        lintConfig file("lint.xml")
        warningsAsErrors true
        textReport true
        textOutput "stdout"
    }

    play {
        jsonFile file('play-keys.json')
        track playTrack
        untrackOld playTrack == 'beta'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:cardview-v7:$supportVersion"
    implementation "com.android.support:support-v13:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "android.arch.core:runtime:$archVersion"
    implementation "android.arch.core:common:$archVersion"
    implementation "com.google.android.gms:play-services-maps:15.0.1"
    implementation "com.google.firebase:firebase-core:16.0.1"
    implementation "com.google.firebase:firebase-database:16.0.1"
    implementation "com.google.firebase:firebase-perf:16.0.0"
    implementation "com.firebaseui:firebase-ui-database:$firebaseUIVersion"
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.github.chrisbanes:PhotoView:2.0.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true
    }
}

apply plugin: 'com.google.gms.google-services'
