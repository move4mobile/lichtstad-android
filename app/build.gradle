plugins {
    id('com.android.application')
    id('com.github.triplet.play') version '2.2.0'
    id('com.google.firebase.crashlytics')
}

def buildNumber = (System.getenv("TRAVIS_BUILD_NUMBER") ?: "444") as Integer
def playTrack = System.getenv('PLAY_TRACK') ?: 'alpha'

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "nl.gramsbergen.oranjevereniging.lichtstad"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode buildNumber
        versionName "2021.1"

        resConfigs "nl"
    }

    signingConfigs {
        release {
            storeFile file("keystore.jks")
            storePassword System.getenv('KEYSTORE_PASSWORD')
            keyAlias "lichtstad"
            keyPassword System.getenv('KEYSTORE_PASSWORD')
        }
    }

    buildTypes {
        debug {
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable false
        }
    }

    buildFeatures {
        dataBinding true
    }

    flavorDimensions "app"
    productFlavors {
        lichtstad {
            dimension "app"
            applicationId "nl.gramsbergen.oranjevereniging.lichtstad"
        }
        dmr {
            dimension "app"
            applicationId "nl.combibaangramsbergen.dmr"
        }
        triathlon {
            dimension "app"
            applicationId "nl.triathlonhardenberg.app"
            buildConfigField 'String', 'EVENT_YEAR', '"2019"'
        }
    }

    lintOptions {
        lintConfig file("lint.xml")
        warningsAsErrors true
        textReport true
        textOutput "stdout"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

play {
    serviceAccountCredentials file('play-keys.json')
    track playTrack
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
    implementation 'androidx.arch.core:core-runtime:2.1.0'
    implementation 'androidx.arch.core:core-common:2.1.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation "com.google.android.gms:play-services-maps:17.0.1"
    implementation 'com.google.maps.android:android-maps-utils:0.5'
    implementation 'com.google.firebase:firebase-core:19.0.0'
    implementation 'com.google.firebase:firebase-database:20.0.1'
    implementation 'com.google.firebase:firebase-messaging:22.0.0'
    implementation "com.firebaseui:firebase-ui-database:8.0.0"
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.github.chrisbanes:PhotoView:2.0.0'
    implementation 'com.squareup.moshi:moshi:1.6.0'
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"

    implementation platform('com.google.firebase:firebase-bom:28.3.1')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
}

apply plugin: 'com.google.gms.google-services'
